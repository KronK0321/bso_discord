:x: vouching is no longer required. Go hog wild.
{//;
{suppresslookup}
{if;{logic;||;{bool;{argslength};<;1};{bool;{argslength};>;2}};
  __**Command Name:**__ {commandname}
  __**Usage:**__ !{commandname} [list] <ally or guest>
  
Use `!help vouch` for usage information.
{return}
}

{set;~corproles;396993641480192000;396993657653428224;516981099260280853;396993660455092224;401486171932196864;406418309454757899}
{set;~allyrole;455134557532848139}
{set;~retiredrole;375989926174523402}
{set;~guestrole;369615227454488576}
{set;~voucherroles;{concat;{get;~corproles};{get;~allyrole};{get;~retiredrole}}}
{set;~allyrsruns;10}
{set;~allydayshere;7}

{if;{lower;{args;0}};startswith;list;
  {if;{argslength};>;1;{set;~subjectuid;{userid;{args;1}}};{set;~subjectuid;{userid}}}
  {if;0{get;~subjectuid};==;0;Couldn't find the specified user. Cancelling.{return}}

  {if;{userhasrole;["{get;~guestrole}","{get;~allyrole}"];{get;~subjectuid}};
    {if;0{get;_{get;~subjectuid}_ally_vouchee;0};
      The following members have vouched for `{usernick;{get;~subjectuid}}`:{newline}- {regexreplace;{foreach;~voucher;{get;_{get;~subjectuid}_ally_vouchee};`{usernick;{get;~voucher}}`,{space}};/, $/;.}
    ;
      {newline}Nobody has vouched for `{usernick;{get;~subjectuid}}`.
    }
  }
  {if;{userhasrole;{get;~voucherroles};{get;~subjectuid}};
    {if;0{get;_{get;~subjectuid}_ally_voucher;0};
      {newline}`{usernick;{get;~subjectuid}}` has vouched for the following members:{newline}- {regexreplace;{foreach;~vouchee;{get;_{get;~subjectuid}_ally_voucher};`{usernick;{get;~vouchee}}`,{space}};/, $/;.}
    ;
      {newline}`{usernick;{get;~subjectuid}}` has not vouched for anyone.
    }
  }
  {return}
}

{set;~voucheruid;{userid}}
{set;~voucheeuid;{userid;{args;0}}}

{if;0{get;~voucheeuid};==;0;
  :x: Couldn't find the specified user. Cancelling.{return}
}

{if;{userhasrole;{get;~voucherroles};{get;~voucheruid}};;:x: Only Order members and Allies can vouch for another member.{return}}

{if;{indexof;{get;_vouch_shit_list};{get;~voucheruid}};>;-1;:x: `{usernick;{get;~voucheruid}}` has been barred from vouching for members.{return}}

{if;{indexof;{get;_vouch_shit_list};{get;~voucheeuid}};>;-1;:x: `{usernick;{get;~voucheeuid}}` has been barred from becoming an ally.{return}}

{if;{userhasrole;{get;~guestrole};{get;~voucheeuid}};;:x: `{usernick;{get;~voucheeuid}}` is not a guest. Only guests can be vouched for.{return}}

{if;{isarray;{get;_{get;~voucheeuid}_ally_vouchee}};;{set;_{get;~voucheeuid}_ally_vouchee;[]}}
{if;{isarray;{get;_{get;~voucheruid}_ally_voucher}};;{set;_{get;~voucheruid}_ally_voucher;[]}}

{if;{indexof;{get;_{get;~voucheeuid}_ally_vouchee};{get;~voucheruid}};>;-1;
  :x: `{usernick;{get;~voucheruid}}` has already vouched for `{usernick;{get;~voucheeuid}}`.{return}
}

{if;{userhasrole;{get;~allyrole};{get;~voucheruid}};
  {set;~diff;{math;-;{time;X};{time;X;{get;_{get;~voucheruid}_ally_date}}}}
  {if;{get;~diff};<;{math;*;{get;~allydayshere};86400};
    {set;~d;{split;{math;/;{get;~diff};86400};.}}
    {set;~d_remain;{math;%;{get;~diff};86400}}
    {set;~h;{split;{math;/;{get;~d_remain};3600};.}}
    {set;~h_remain;{math;%;{get;~d_remain};3600}}
    {set;~m;{split;{math;/;{get;~h_remain};60};.}}
    {set;~m_remain;{math;%;{get;~h_remain};60}}
    :x: Members must be an ally for {get;~allydayshere} days before they can vouch for another member. `{usernick;{get;~voucheruid}}` has only been an ally for {if;{get;~d;0};>;0;{get;~d;0} day{if;{get;~d;0};!=;1;s};{if;{get;~h;0};>;0;{get;~h;0} hour{if;{get;~h;0};!=;1;s};{if;{get;~m;0};>;0;{get;~m;0} minute{if;{get;~m;0};!=;1;s};{get;~m_remain} second{if;{get;~m_remain};!=;1;s}}}}.{set;~allyerror;1}
  }
  {set;~userruns;{math;+;{get;_RS5c{get;~voucheruid}};{get;_RS6c{get;~voucheruid}};{get;_RS7c{get;~voucheruid}};{get;_RS8c{get;~voucheruid}};{get;_RS9c{get;~voucheruid}};{get;_RS10c{get;~voucheruid}}}}
{if;{get;~userruns};<;{get;~allyrsruns};:x: Allies must have at least {get;~allyrsruns} RS runs with BSO before they can vouch for another member. `{usernick;{get;~voucheruid}}` has only run {get;~userruns}.{set;~allyerror;1}
  }
  {if;0{get;~allyerror};{return}}
}

{set;~confmsg1;{usermention}, you are about to endorse {usermention;{get;~voucheeuid}} for ally status at Black Star Order.{newline}{newline}The quality of BSO Red Star runs depends on members like you endorsing only responsible, team-oriented players to run with us. By now, you should have had several good RS experiences with {usermention;{get;~voucheeuid}} and are comfortable with **forever** being known as the one who vouched for them.}

{set;~confmsg2;{newline}{newline}Type `OK` now to confirm or `cancel` to abort. {newline}{newline}This message will self-destruct in 30s.}

{set;~confmsgid;{send;{channelid};{get;~confmsg1}{get;~confmsg2}}}

{set;~msgid;{waitmessage;{channelid};{userid};{switch;{lower;{messagetext}};["ok","cancel"];true;false};30}}

{switch;{lower;{messagetext;{get;~msgid;1}}};
ok;
{set;~confmsg2;{newline}{newline}:ballot_box_with_check: Recorded `{usernick;{get;~voucheruid}}` as vouching for `{usernick;{get;~voucheeuid}}`.}
{push;{get;_{get;~voucheeuid}_ally_vouchee};{get;~voucheruid}}
{push;{get;_{get;~voucheruid}_ally_voucher};{get;~voucheeuid}}
;
cancel;
{set;~confmsg2;{newline}{newline}:x: Confirmation cancelled by {usermention;{get;~voucheruid}}.}
;
{set;~confmsg2;{newline}{newline}:x: Timed out waiting for a reply from {usermention;{get;~voucheruid}}.}
}

{edit;{channelid};{get;~confmsgid};{get;~confmsg1}{get;~confmsg2}}
{get;~confmsg2}
  
  {//; todo
vouch

 		- only members and allies can vouch for someone
 		- can only vouch for guests
 		- store who vouched FOR you in a variable						_<uid>_ally_vouchee
 		- store who you have vouched for in a different var  _<uid>_ally_voucher

 - have a shit list that prevents someone from being vouched for or to be !allied no matter how many vouches they have
 		- have !ally check the vouch list before allowing
 		- are the current vouchers still on the server

 		- list based on role
 - current allies have to be here for a length of time before they can vouch? 7 days + 10 runs
 		- make sure can't double vouch
  }
}