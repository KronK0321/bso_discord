{//; Changelog
2018-10-27 Command created
2018-11-27 added history and display functions. Started logging individual deaths.
2018-12-10 added allies, forbid guests

!cc sethelp idied __**Usage:**__ !idied [ <number of deaths> | history ]

Adds your death count to the running tally of BSO death counts. 

Usage:
**!idied** - Views the current counter
**!idied 5** - Adds 5 *of your own* deaths to the counter
**!idied history** - Shows stats from the previous collection periods
}
{set;~resetroles;447472629146714166;372154093910622220} {//; bot-dev, admin}
{set;~corproles;396993641480192000;396993657653428224;516981099260280853;396993660455092224;401486171932196864;406418309454757899}
{if;{userhasrole;{get;~corproles}};{set;~isbso;1};{set;~isbso;0}}
{if;{userhasrole;455134557532848139};{set;~isally;1};{set;~isally;0}}

{if;{logic;&&;{bool;{get;~isbso};==;0};{bool;{get;~isally};==;0}};:x: Only members and allies may view and add deaths to the tally.{return}}

{if;0{length;{get;_squishie_deaths}};;{set;_squishie_deaths;0}}
{if;0{length;{get;_ally_squishie_deaths}};;{set;_ally_squishie_deaths;0}}
{if;0{length;{get;_{userid}_squishie_deaths}};;{set;_{userid}_squishie_deaths;0}}
{if;{argslength};
  {switch;{lower;{args}};
    history;
      {for;~i;1;<;{length;{get;_squishie_deaths_history}};
          {set;~monthend;{get;_squishie_deaths_history;{get;~i}}}
{get;_squishie_deaths_history;{math;-;{get;~i};1}} - {get;~monthend} -- Member:  `{get;_squishie_deaths_{get;~monthend}}`  Ally: {get;_ally_squishie_deaths_{get;~monthend}}
      }
    ;
    reset;
      {if;{userhasrole;{get;~resetroles}};;:x: Only the following roles may reset the counter: {regexreplace;{foreach;~role;{get;~resetroles};{rolename;{get;~role}},{space}};/, $/;.}{return}}
      {set;~monthend;{time;YYYY-MMM-DD}}
:ballot_box_with_check: Recording reset date as {get;~monthend}
      {push;{get;_squishie_deaths_history};{get;~monthend}}
      {set;_squishie_deaths_{get;~monthend};{get;_squishie_deaths}}
      {set;_ally_squishie_deaths_{get;~monthend};{get;_ally_squishie_deaths}}
:ballot_box_with_check: Adding current period total of Member: `{get;_squishie_deaths}` and Ally: `{get;_ally_squishie_deaths}` to tracked history.
:ballot_box_with_check: Resetting current period total to `0`.
      {set;_squishie_deaths;0}
      {set;_ally_squishie_deaths;0}
    ;
    {//; not history or reset}
      {if;{parseint;{args;0}};==;NaN;
:x: invalid option: {args;0}. Use `!help idied` for usage info!{return}
      }
      {if;{parseint;{args;0}};>;0;
        {if;{get;~isally};
          {set;~deaths;{args;0}}
          {set;_ally_squishie_deaths;{math;+;{get;_ally_squishie_deaths};{get;~deaths}}}
:skull_crossbones: Recorded `{get;~deaths}` more Ally squishy death{if;{get;~deaths};>;1;s} for a total of `{get;_ally_squishie_deaths}` since {get;_squishie_deaths_history;{math;-;{length;{get;_squishie_deaths_history}};1}}.
        {set;_{userid}_squishie_deaths;{math;+;0{get;_{userid}_squishie_deaths};{get;~deaths}}}
That's `{get;_{userid}_squishie_deaths}` total for {usernick;{userid}} since 2018-Dec-10.
        ;
          {set;~deaths;{args;0}}
          {set;_squishie_deaths;{math;+;{get;_squishie_deaths};{get;~deaths}}}
:skull_crossbones: Recorded `{get;~deaths}` more Member squishy death{if;{get;~deaths};>;1;s} for a total of `{get;_squishie_deaths}` since {get;_squishie_deaths_history;{math;-;{length;{get;_squishie_deaths_history}};1}}.
          {set;_{userid}_squishie_deaths;{math;+;0{get;_{userid}_squishie_deaths};{get;~deaths}}}
That's `{get;_{userid}_squishie_deaths}` total for {usernick;{userid}} since 2018-Nov-27.
       }
     ;
:x: Must specify a positive number of deaths. {return}
     }
  }
;
:skull_crossbones: A total of `{get;_squishie_deaths}` Member and `{get;_ally_squishie_deaths}` Ally squishies have died since {get;_squishie_deaths_history;{math;-;{length;{get;_squishie_deaths_history}};1}}. :skull_crossbones:
  
You've recorded `{get;_{userid}_squishie_deaths}` personal squishie deaths since {if;{get;~isally};2018-Dec-10;2018-Nov-27}, {usernick}.
}

