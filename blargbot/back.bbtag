{//;Changelog
2019-01-29 - Added clearing of AFK reason
}

{//;{if;{argslength};>;0;
  __**Command Name:**__ {commandname}
  __**Usage:**__ !{commandname}{return}
}}

{//; How far into being AFK should the grace period be before setting auto-back_}
{if;0{length;{get;_{userid}_afk_autoback}};{set;~threshold;{get;_{userid}_afk_autoback}};{set;~threshold;0.50}} 

{set;_interval_send_count;0}
{set;~rsrole;508381265657856015}
{set;~afkchannel;501593412634542100}
{set;~publicroles;369615227454488576;455134557532848139}
{execcc;wsinit;g}
{set;~mainwsTags;{get;_ws-tag-main}}
{set;~altwsTags;{get;_ws-tag-alts}}
{set;~maincorpNicks;{get;_ws-corp-nick-main}}
{set;~altcorpNicks;{get;_ws-corp-nick-alts}}
{set;~maincorpIDs;{get;_ws-role-main}}
{set;~altcorpIDs;{get;_ws-role-alts}}
{set;~wscmdschan;{get;_ws-chan-comm-main}}

{if;{userhasrole;{get;~publicroles}};{set;_{userid}_afk_notify;dm};{if;{length;{get;_{userid}_afk_notify}};==;0;{set;_{userid}_afk_notify;chan}}} {//; set default location for afk-messages}

{switch;{lower;{commandname}};
["back","jback"];
  {set;~backmsg;{if;0{get;_{userid}_afk};Ok, {usermention}, you are no longer marked as being AFK{if;{get;_{userid}_afk_notify};==;dm;{space}on BSO discord}.{newline}{newline}You came back {if;{time;X;{get;_{userid}_afk}};>;{time;X};earlier;later} than expected.{if;0{get;_{userid}_seentimes};{newline}You were queried in !seen {get;_{userid}_seentimes} time{if;{get;_{userid}_seentimes};!=;1;s} while you were gone.}{if;0{get;_{userid}_has_rs};{newline}Your access to #bso-rs has been restored.};{usermention}, you weren't marked as being AFK{if;{get;_{userid}_afk_notify};==;dm;{space}on BSO discord}!{if;0{get;_{userid}_has_rs};{newline}Your access to #bsr-rs has been restored.}}}
  {switch;{get;_{userid}_afk_notify};
    dm;
      {dm;{userid};{get;~backmsg}}
    ;
    chan;
      {void;{send;{get;~afkchannel};{get;~backmsg}}}
    ;
    none;
    ;
  }
  {delete}
;
  {//; called by everything autoresponse}
  {set;~diff;{math;-;{time;X;{get;_{userid}_afk}};{time;X;{get;_{userid}_afk_set}}}}
  {if;{math;+;{time;X;{get;_{userid}_afk_set}};{math;*;{get;~diff};{get;~threshold}}};<;{time;X};
    {set;~backmsg;{usermention}, you are no longer marked as being AFK{if;{get;_{userid}_afk_notify};==;dm;{space}on BSO discord}.{newline}{newline}You came back {if;{time;X;{get;_{userid}_afk}};>;{time;X};earlier;later} than expected.{if;0{get;_{userid}_seentimes};{newline}You were queried in !seen {get;_{userid}_seentimes} time{if;{get;_{userid}_seentimes};!=;1;s} while you were gone.}{if;0{get;_{userid}_has_rs};{newline}Your access to #bso-rs has been restored.}}
    {switch;{get;_{userid}_afk_notify};
      ["dm","none"];
        {dm;{userid};{get;~backmsg}}
      ;
      chan;
        {void;{send;{get;~afkchannel};{get;~backmsg}}}{set;_interval_send_count;1}
      ;
    }
  ;
  {return}
  }
}

{usernick} is no longer AFK.{if;{length;{get;_{userid}_afk_msg}};{space}AFK reason was: {get;_{userid}_afk_msg}.}

{if;{indexof;{get;_afk_list};{userid}};>;-1;{void;{splice;{get;_afk_list};{indexof;{get;_afk_list};{userid}};1}}}
{set;_{userid}_seentimes}
{set;_{userid}_afk}
{set;_{userid}_afk_msg}

{if;0{get;_{userid}_has_rs};
  {if;{addrole;{get;~rsrole};{userid}};
    {set;_{userid}_has_rs;0}
  }
}

{if;0{get;_interval_send_count};;
  {if;{userhasrole;{get;~maincorpIDs}};
    {foreach;~role;{userroles;{get;~userid}};
      {set;~wsindex;{indexof;{get;~maincorpIDs};{get;~role}}}
      {if;{get;~wsindex};>;-1;
        {set;~corpchan;{get;~wscmdschan;{get;~wsindex}}}
        {if;0{get;_{get;~corpchan}_status_msg};{void;{delete;{get;~corpchan};{get;_{get;~corpchan}_status_msg}}}}
        {set;_{get;~corpchan}_status_msg;{send;{get;~corpchan};{execcc;wstimes;{get;~mainwsTags;{get;~wsindex}}}}}
        {set;_{get;~corpchan}_status_msg_time;{time}}
      }
    }
  }
}